plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id("maven-publish")
}

group 'dev.selena'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://maven.enginehub.org/repo/"
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
}

dependencies {
    implementation('com.google.code.gson:gson:2.10.1')
    implementation("com.google.guava:guava:31.1-jre")
    implementation("de.tr7zw:item-nbt-api:2.10.0")
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
}

build {
    dependsOn shadowJar
}

shadowJar {

    dependsOn(jar)
    if (System.getenv("BUILD_NUMBER") == null) {
        archiveName = rootProject.name + "-" + version + ".jar"
    } else {
        archiveName = rootProject.name + "-" + version + "-b" + System.getenv("BUILD_NUMBER") + ".jar"
    }

    dependencies {
        include(dependency('com.google.code.gson:gson'))
        include(dependency('de.tr7zw:item-nbt-api'))
        include(dependency('com.google.guava:guava'))
    }
    relocate "com.google", "dev.selena.libs"
    relocate "de.tr7zw.changeme", "dev.selena.libs"

    zip64 true
    subprojects.each { subproject ->
        from subproject.sourceSets.main.output.classesDirs
        from subproject.sourceSets.main.output.resourcesDir
    }

    configurations = [project.configurations.getByName("runtimeClasspath")]
}

publishing {
    repositories {
        maven {
            name = "GlitchAPI"
            url = uri("https://maven.pkg.github.com/RedW0lfStoneYT/GlitchAPI")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
